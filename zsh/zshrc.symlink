# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# your project folder that we can `c [tab]` to
export PROJECTS=~/Code

fpath=(/usr/local/share/zsh/site-functions $fpath)
fpath=(/usr/local/share/zsh-completions $fpath)

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load everything but the path and completion files
#set -x
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done
#set +x

# load the path files
#set -x
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done
#set +x

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files


# The next line updates PATH for the Google Cloud SDK.
source '/Users/mnbbrown/google-cloud-sdk/path.zsh.inc'

# The next line enables bash completion for gcloud.
source '/Users/mnbbrown/google-cloud-sdk/completion.zsh.inc'

# Proper locale
: ${LANG:="en_US.UTF-8"}
: ${LANGUAGE:="en"}
: ${LC_CTYPE:="en_US.UTF-8"}
: ${LC_ALL:="en_US.UTF-8"}
export LANG LANGUAGE LC_CTYPE LC_ALL
